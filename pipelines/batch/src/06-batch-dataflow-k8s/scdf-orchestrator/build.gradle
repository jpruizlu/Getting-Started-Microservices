plugins {
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'com.google.cloud.tools.jib' version '1.0.2'
	id 'org.liquibase.gradle' version '2.0.1'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

apply from: 'gradle/liquibase.gradle'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation "org.springframework.cloud:spring-cloud-dataflow-rest-client:${scdfRestClientVersion}"
	implementation 'org.liquibase:liquibase-core'
	liquibaseRuntime 'org.liquibase:liquibase-core'
	liquibaseRuntime "org.liquibase.ext:liquibase-hibernate5:${liquibaseHibernate5Version}"
	liquibaseRuntime 'org.postgresql:postgresql'
	liquibaseRuntime sourceSets.main.compileClasspath
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'com.h2database:h2'
}

jib {
	from {
		image = 'openjdk:8u201-jre-alpine'
	}
	to {
		image = "registry.hub.docker.com/jsa4000/dataflow-${project.name}"
		credHelper = 'osxkeychain'
		tags = [version, 'latest']
	}
	container {
		jvmFlags = ['-XX:+UnlockExperimentalVMOptions',
					'-XX:+UseCGroupMemoryLimitForHeap',
					'-XX:+HeapDumpOnOutOfMemoryError',
					'-XX:MaxRAMFraction=2']
		ports = ['8080']
		useCurrentTimestamp = true
		labels = [ownerName: 'jsa4000',
				  projectName: project.name,
				  creationDate: System.nanoTime().toString()]
		//format = 'OCI'
	}
}